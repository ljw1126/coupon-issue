buildscript {
    ext {
        restdocsApiSpecVersion = '0.19.4'
    }
}

plugins {
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

dependencies {
    implementation project(":coupon-core")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"

    swaggerUI 'org.webjars:swagger-ui:4.11.1'
}

test {
    useJUnitPlatform()
}

openapi3 {
    server = "http://localhost:8080"
    title = "restdocs-swagger Test API Documentation"
    description = "Spring REST Docs with SwaggerUI"
    version = "0.0.1"
    format = "yaml"
}

postman {
    title = "restdocs-swagger Test API Documentation"
    version = "0.0.1"
    baseUrl = "http://localhost:8080"
}

swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

bootJar {
    dependsOn 'openapi3'
    dependsOn 'generateSwaggerUISample'

    tasks.findByName("generateSwaggerUISample").mustRunAfter("openapi3")

    from("${generateSwaggerUISample.outputDir}") {
        into 'static/docs'
    }
}
